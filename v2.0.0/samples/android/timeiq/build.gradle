// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {

        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

subprojects {
    afterEvaluate {project ->
        if (project.hasProperty("android")) {
            android {
                compileSdkVersion 21
                buildToolsVersion "23.0.2"
                defaultConfig {
                    minSdkVersion 21
                    targetSdkVersion 21
                    versionCode 1
                    versionName "1.0.03"

                    multiDexEnabled true
                }
                dexOptions {
                    javaMaxHeapSize "4g"
                }
            }
        }
    }
}


allprojects {
    repositories {
        jcenter()

        maven { url "${rootProject.projectDir}/../../../sdk/android/maven-repo/" }
    }
    tasks.withType(JavaCompile) {
        sourceCompatibility = "1.7"
        targetCompatibility = "1.7"
    }



    afterEvaluate {
        if (getPlugins().hasPlugin('android') ||
                getPlugins().hasPlugin('android-library')) {

            println name // for debugging

            configure(android.lintOptions) {
                abortOnError false
            }
        }

    }
}


// Log timings per task.
class TimingsListener implements TaskExecutionListener, BuildListener {
    private Clock clock
    private timings = []

    @Override
    void beforeExecute(Task task) {
        clock = new org.gradle.util.Clock()
    }

    @Override
    void afterExecute(Task task, TaskState taskState) {
        def ms = clock.timeInMs/1000
        timings.add([ms, task.path])
        task.project.logger.info "${task.path} took ${ms} sec"
    }

    @Override
    void buildFinished(BuildResult result) {
        println "Task timings:"
        for (timing in timings) {
            if(timing[0] > 3)
                printf "%7s sec  %s\n", timing

        }
    }

    @Override
    void buildStarted(Gradle gradle) {}

    @Override
    void projectsEvaluated(Gradle gradle) {}

    @Override
    void projectsLoaded(Gradle gradle) {}

    @Override
    void settingsEvaluated(Settings settings) {}
}

gradle.addListener new TimingsListener()

task clean(type: Delete) {
    delete rootProject.buildDir
}
